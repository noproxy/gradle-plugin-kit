/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

initscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-jvm:2.3.0"
    }
}

settingsEvaluated { Settings settings ->
    if (settings.findProject(':docs')) {
        // do nothings
        return
    }

    settings.include(':docs')
    def docs = new File(settings.rootDir, 'docs')
    if (!docs.exists()) {
        docs.mkdirs()
    }

    beforeProject { Project project ->
        if (project.path != ':docs') {
            return
        }

        project.repositories {
            jcenter()
        }
        project.plugins.apply(org.asciidoctor.gradle.jvm.AsciidoctorJPlugin)
        project.asciidoctor {

            baseDirFollowsSourceDir()
            sourceDir project.properties.getOrDefault('dir', '.')
            sources {
                include project.properties.getOrDefault('sources', 'index*.adoc')
            }
            attributes(
                    toc: 'left',
                    toclevels: 2,
                    'source-highlighter': 'coderay',
                    imagesdir: './images',
                    icons: 'font',
            )
            // translation
            attributes(
                    lang: 'zh',
                    'appendix-caption': '附录',
                    'caution-caption': '注意',
                    'example-caption': '示例',
                    'figure-caption': '图表',
                    'important-caption': '重要',
                    'last-update-label': '最后更新',
                    'listing-caption': '列表',
                    'manname-title': '名称',
                    'note-caption': '笔记',
                    'preface-title': '序言',
                    'table-caption': '表格',
                    'tip-caption': '提示',
                    'toc-title': '目录',
                    'untitled-label': '暂无标题',
                    'version-label': '版本',
                    'warning-caption': '警告',
            )
        }
    }
}



