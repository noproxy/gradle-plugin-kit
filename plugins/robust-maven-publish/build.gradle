/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'io.github.noproxy.android-plugin-kit-plugin' version '0.0.2'
}

repositories {
    maven {
        name = "xal"
        url = 'https://maven.apuscn.com/nexus/content/repositories/android-xal-releases/'
    }
    maven {
        name = "xal snapshot"
        url = 'https://maven.apuscn.com/nexus/content/repositories/android-xal-snapshots/'
    }
}

dependencies {
    implementation(gradleApi())
    implementation("org.jetbrains:annotations:13.0")
    implementation("org.apache.commons:commons-lang3:3.9")
    implementation("com.xal.robust:gradle-plugin:1.0.0-beta01")
    implementation("com.xal.robust:auto-patch-plugin:1.0.0-beta01")
}

repositories {
    google()
}

dependencies {
    implementation("com.android.tools.build:gradle:4.0.2")
}

// publishing
assert ("io.github.noproxy" == group)
assert ("robust-maven-publish" == name)
version = "1.0.0-beta03"

apply from: rootProject.file("gradle/publishing.gradle")

githubRelease {
    body "Incubating release."
}

gradlePlugin {
    plugins {
        create("robustMavenPublish") {
            id = "${group}.${project.name}"
            implementationClass = "io.github.noproxy.plugin.robust.RobustMavenPublishPlugin"
        }
    }
}

pluginBundle {
    website = "https://noproxy.github.io/robust-maven-publish"
    vcsUrl = "https://github.com/noproxy/robust-maven-publish"
    description = "A plugin to publish artifacts(android apk, mapping.txt and R.txt) required by Meituan Robust."
    tags = ["publish", "maven", "android"]

    plugins {
        robustMavenPublish {
            displayName = "Robust Maven Publish plugin"
        }
    }
}

apply from: rootProject.file('gradle/test_plugin.gradle')