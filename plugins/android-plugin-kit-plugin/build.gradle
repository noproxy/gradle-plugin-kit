/*
 *    Copyright 2019 the original author or authors.
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

dependencies {
    implementation(gradleApi())
    implementation("org.jetbrains:annotations:13.0")
    implementation("org.apache.commons:commons-lang3:3.9")

    testImplementation(project(":test-kit-ext"))
}

repositories {
    google()
}

dependencies {
    compileOnly("com.android.tools.build:gradle:3.3.2")
}

tasks.withType(Test) {
    doFirst {
        file("build/tmp/testfiles").deleteRecursively()
    }
}
// publishing
assert ("android-plugin-kit-plugin" == name)
assert ("io.github.noproxy" == group)
version = "0.0.1"

apply from: rootProject.file("gradle/publishing.gradle")

githubRelease {
    body "Incubating release."
}

gradlePlugin {
    plugins {
        create("androidPluginKitPlugin") {
            id = "${group}.${project.name}"
            implementationClass = "com.github.noproxy.android.plugin.AndroidPluginKitPlugin"
        }
    }
}

pluginBundle {
    website = "https://noproxy.github.io/${rootProject.name}"
    vcsUrl = "https://github.com/noproxy/${rootProject.name}"
    description = "A plugin to help developing gradle plugin based on android Gradle Plugin."
    tags = ["gradle", "gradle plugin", "android"]

    plugins {
        androidPluginKitPlugin {
            displayName = "Plugin for Android Plugin Kit"
        }
    }
}