/*
 *    Copyright 2019 the original author or authors.
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    group = "build"
    archiveClassifier.set("sources")
    from(sourceSets.main.allSource) {
        include("**/api/**.java")
    }
    dependsOn(tasks.named("classes"))
}

task javadocJar(type: Jar) {
    group = "build"
    archiveClassifier.set("javadoc")
    def javadoc = tasks.getByName("javadoc") as Javadoc
    from(javadoc.destinationDir)
    dependsOn(javadoc)
}

task groovydocJar(type: Jar) {
    group = "build"
    archiveClassifier.set("groovydoc")
    def groovydoc = tasks.getByName("groovydoc") as Groovydoc
    from(groovydoc.destinationDir)
    dependsOn(groovydoc)
}

publishing {
    repositories {
        maven {
            name = "BuildDir"
            url = uri(rootProject.file("build/repo"))
        }
    }
    publications.withType(MavenPublication).all {
        artifact(tasks.getByName("groovydocJar"))
        artifact(tasks.getByName("javadocJar"))
        artifact(tasks.getByName("sourcesJar"))

        if (name != "pluginMaven") {
            artifact(tasks.getByName("jar"))
        }
    }

}

apply plugin: "com.github.breadmoirai.github-release"

githubRelease {
    if (project.hasProperty('github.publish.token')) {
        token getProperty('github.publish.token')
    } else {
        logger.warn("set 'github.publish.token' in gradle.properties to publish github releases")
    }
    repo rootProject.name
    draft byProperty("draft")
    prerelease isPreRelease()
    overwrite true
    releaseAssets jar, sourcesJar, javadocJar, groovydocJar
    body "Incubating release."
}

def isPreRelease() {
    def version = project.version.toString()
    return version.contains('-') || version.startsWith('0')
}

def byProperty(String key, boolean defaultValue = true) {
    def value = properties.getOrDefault(key, defaultValue)
    return Boolean.parseBoolean(value.toString())
}

/*
Usage:

// register any publication to publish to maven repository
publishing {
    publications {
        lib(MavenPublication)
    }
}

// set repo name and release note to publish to github releases
githubRelease {
    repo 'name'
    body "Incontributing release."
}
 */